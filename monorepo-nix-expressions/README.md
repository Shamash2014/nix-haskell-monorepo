
# Monorepo Nix Expressions

Now it's time to put our haskell code to work.
Each of our haskell packages has to have a corresponding nix expression describing how to build it.
The process of writing Nix expressions for haskell packages is error-prone and they would likely fall
out of sync so we would like to generate those Nix files.

To achieve this I use a very simple bash script that expects a directory as an argument.
It will first search for all the cabal files in the directory and it will generate one nix expression file for each of them using the `cabal2nix` utility.

```bash
$ cd monorepo
$ tree .
.
├── code
│   ├── hello-world
│   │   ├── exe
│   │   │   └── Main.hs
│   │   └── hello-world.cabal
│   └── universe
│       ├── src
│       │   └── Universe
│       │       └── World.hs
│       └── universe.cabal
└── nix
    ├── generate-packages.sh
    ├── pinned-nixpkgs.nix
    └── release.nix

$ cd nix
$ ./generate-packages.sh ../code
Clearing folder /Users/fghibellini/code/nix-haskell-monorepo/monorepo-nix-expressions/monorepo/nix/packages
Reading packages in /Users/fghibellini/code/nix-haskell-monorepo/monorepo-nix-expressions/monorepo/code
Generating cabal for package: hello-world (dir: /Users/fghibellini/code/nix-haskell-monorepo/monorepo-nix-expressions/monorepo/code/hello-world)
Generating cabal for package: universe (dir: /Users/fghibellini/code/nix-haskell-monorepo/monorepo-nix-expressions/monorepo/code/universe)

$ cd ..
$ tree .
.
├── code
│   ├── hello-world
│   │   ├── exe
│   │   │   └── Main.hs
│   │   └── hello-world.cabal
│   └── universe
│       ├── src
│       │   └── Universe
│       │       └── World.hs
│       └── universe.cabal
└── nix
    ├── generate-packages.sh
    ├── hydra.nix
    ├── packages
    │   ├── hello-world.nix
    │   └── universe.nix
    ├── packages.nix
    ├── pinned-nixpkgs.nix
    └── release.nix

$ cat nix/packages/hello-world.nix
{ mkDerivation, aeson, base, stdenv, universe }:
mkDerivation {
  pname = "hello-world";
  version = "0.1.0.0";
  src = ../../code/hello-world;
  isLibrary = false;
  isExecutable = true;
  executableHaskellDepends = [ aeson base universe ];
  license = stdenv.lib.licenses.unfree;
  hydraPlatforms = stdenv.lib.platforms.none;
}

$ cat nix/packages.nix
# DO NOT MODIFY ! - this file is generated by generate-packages.sh
{
    hello-world = import ./packages/hello-world.nix;
    universe = import ./packages/universe.nix;
}
```

As you can see it also generated a `packages.nix` file that just imports all of the above files and bundles them in an attribute set for easier consumption.
This file is not strictly necessary as Nix allows you to scan directories for files but I find it makes the expressions more readable.

Assuming our packages really do depend only on each other or their dependencies can be satisfied with packages from our pinned nixpkgs, we can already take them for a spin.
We will build our packages by inserting them into the haskell package set of Nixpkgs.
Nixpkgs accepts a config argument that (among other things) allows us to override packages in a functional (non-destructive) manner.
We override `haskellPackages` to be the package set for `ghc844` but before assigning it we also modify the set by adding our own packages.

```nix
# release.nix
let

  nixpkgs = import (import ./pinned-nixpkgs.nix) { inherit config; };

  monorepo-pkgs = import ./packages.nix;

  config = {
    allowUnfree = true;
    packageOverrides = pkgs: rec {
      haskellPackages = pkgs.haskell.packages.ghc844.override {
        overrides = self: super: builtins.mapAttrs (name: value: super.callPackage value {}) monorepo-pkgs;
      };
    };
  };

in nixpkgs
```

The `nixpkgs` value we return now from `release.nix` is the exact same package set as before,
but instead of having `haskellPackages` pointing to a pristine package set for `ghc-8.6.4` <sup>[1](#footnote-1)</sup>, it points to our
modified package set for ghc-8.4.4. `haskell.packages.ghc844` still points to the pristine package set for `ghc-8.4.4` - just compare:

```bash
$ cd nix
$ nix-instantiate --eval -E '(import ./release.nix).haskellPackages.hello-world.pname'
"hello-world"
$ nix-instantiate --eval -E '(import ./release.nix).haskell.packages.ghc844.hello-world.pname'
error: attribute 'hello-world' missing, at (string):1:1
(use '--show-trace' to show detailed location information)
```

Now we can build and execute our package by running:

```bash
$ nix-build -A hello-world ./hydra.nix
$ ./result/bin/hello-world-exe
Hello WORLD!!!
```

Or in an ephemeral shell environment:

```bash
$ nix-shell -p '((import ./release.nix).haskellPackages.hello-world)' --command hello-world-exe
Hello WORLD!!!
```

Or use it as a haskell dependency:

```bash
$ nix-shell -p '((import ./release.nix).haskellPackages.ghcWithPackages (pkgs: [ pkgs.universe ]))' --command "bash -c 'ghc-pkg list | grep universe'"
    universe-0.2.0.0
```

# Gitignore files

You might get the following warning when building your projects:

```
warning: dumping very large path (> 256 MiB); this may run out of memory
```

This is caused by the `src` attribute in our packages' expressions:

```
{ mkDerivation, aeson, base, stdenv, universe }:
mkDerivation {
  pname = "hello-world";
  version = "0.1.0.0";
  src = ../../code/hello-world; # <----- THIS ATTRIBUTE
  isLibrary = false;
  isExecutable = true;
```

When Nix evaluates the expression for your package, it will force the attribute `src` which is a Nix path.
Nix paths are copied into the Nix store and they evaluate to the store path. Before performing the
copying though it will compute the SHA hash of the whole tree and check if it's not already present (if this was the case the path would be reused).

This is problematic for 2 reasons:
1. currently there is a [bug](https://github.com/NixOS/nix/issues/358) that causes the SHA computation to run in non-constant memory space
2. any files in the source tree that don't really represent source code are also taken into account.
   An example are `.stack-work` folders, you might potentially have one for each package and they are typically huge, as they contain all build artefacts.

A way to mitigate this is modifying the `src` attribute to take only the desired files into account.
We can patch the output of `cabal2nix` to apply [nix-gitignore](https://github.com/siers/nix-gitignore) on the `src` attribute.

Sed is our friend in such situations:

```bash
# this prepends the path in a Nix expression's `src` attribute with a call to `nix-gitignore.gitignoreSourcePure`
# while also adding nix-gitignore as a dependency
function gitignore-src() {
  sed -i '1s/{ /{ nix-gitignore, / ; /^\s*src = / s/src = /src = nix-gitignore.gitignoreSourcePure [ ..\/..\/..\/.gitignore ] /' "$1"
}
```

This will transform the `hello-world` expression to:

```
{ nix-gitignore, mkDerivation, aeson, base, stdenv, universe }:
mkDerivation {
  pname = "hello-world";
  version = "0.1.0.0";
  src = nix-gitignore.gitignoreSourcePure [ ../../../.gitignore ] .././code/hello-world;
  isLibrary = false;
  isExecutable = true;
  executableHaskellDepends = [ aeson base universe ];
  license = stdenv.lib.licenses.unfree;
  hydraPlatforms = stdenv.lib.platforms.none;
}
```

In the [next chapter](../extra-deps) we will see how to override third-party haskell dependencies.

<a id="footnote-1"><b>[1]</b></a> You can check the default by running `nix-instantiate --eval -E '(import (import ./pinned-nixpkgs.nix) {}).haskellPackages.ghc.version'`

